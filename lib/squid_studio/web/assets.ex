defmodule SquidStudio.Web.Assets do
  @moduledoc """
  Serves the compiled Squid Studio assets from the embedding application's route.
  """

  @behaviour Plug

  import Plug.Conn

  @static_path Application.app_dir(:squid_studio, ["priv", "static"])

  @external_resource css_path = Path.join(@static_path, "app.css")
  @css File.read!(css_path)

  @external_resource js_path = Path.join(@static_path, "app.js")
  @js File.read!(js_path)

  for {key, val} <- [css: @css, js: @js] do
    md5 = Base.encode16(:crypto.hash(:md5, val), case: :lower) |> String.slice(0, 8)

    def current_hash(unquote(key)), do: unquote(md5)
  end

  @impl Plug
  def init(asset), do: asset

  @impl Plug
  def call(conn, :css) do
    %{"md5" => md5} = conn.params
    serve_asset(conn, :css, md5, @css, "text/css")
  end

  def call(conn, :js) do
    %{"md5" => md5} = conn.params
    serve_asset(conn, :js, md5, @js, "application/javascript")
  end

  defp serve_asset(conn, type, requested_md5, content, content_type) do
    if valid_md5?(requested_md5) and requested_md5 == current_hash(type) do
      conn
      |> put_resp_content_type(content_type)
      |> put_resp_header("cache-control", "public, max-age=31536000, immutable")
      |> put_private(:plug_skip_csrf_protection, true)
      |> send_resp(200, content)
    else
      send_resp(conn, 404, "Not Found")
    end
  end

  defp valid_md5?(md5) when is_binary(md5) do
    String.match?(md5, ~r/^[a-f0-9]{8}$/)
  end

  defp valid_md5?(_md5), do: false
end
